dnl  -*- Mode: M4 -*-
dnl
dnl --------------------------------------------------------------------
dnl configure.in --- GNU autoconf source for toplevel directory.
dnl
dnl Copyright (C) 1997-2003 Bruce Korb
dnl
dnl Time-stamp:      "2003-12-07 11:02:10 bkorb"
dnl Author:           Bruce Korb <bkorb@gnu.org>
dnl Maintainer:       Bruce Korb <bkorb@gnu.org>
dnl Created:          Mon Jun 30 15:10:13 1997
dnl            by: bkorb
dnl --------------------------------------------------------------------
dnl Code: $Id: configure.in,v 3.21 2003/12/27 15:06:39 bkorb Exp $
dnl --------------------------------------------------------------------
dnl
AC_INIT([autogen],[5.4],[autogen-bugs@lists.sf.net])
AC_CONFIG_SRCDIR(agen5/autogen.c)
AC_CONFIG_AUX_DIR(config)
AC_CANONICAL_TARGET
. $srcdir/VERSION
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AC_LIBTOOL_WIN32_DLL    m4_define(AC_PROVIDE_AC_LIBTOOL_WIN32_DLL)
ifdef([AC_REVISION],AC_REVISION($Revision: 3.21 $),)dnl

# ----------------------------------------------------------------------
# Substitute VERSION vars here, so that they can be used by the Makefile
# ----------------------------------------------------------------------
AC_SUBST(AG_VERSION)
AC_SUBST(AG_MAJOR_VERSION)
AC_SUBST(AG_MINOR_VERSION)
AC_SUBST(AO_CURRENT)
AC_SUBST(AO_REVISION)
AC_SUBST(AO_AGE)
AC_SUBST(GO_CURRENT)
AC_SUBST(GO_REVISION)
AC_SUBST(GO_AGE)
AC_DEFINE_UNQUOTED(AO_CURRENT,$AO_CURRENT,
                   [Define this to the autoopts current interface number])
AC_DEFINE_UNQUOTED(AO_REVISION,$AO_REVISION,
                   [Define this to the autoopts interface revision number])
AC_DEFINE_UNQUOTED(AO_AGE,$AO_AGE,
                   [Define this to the autoopts interface age number])

# ----------------------------------------------------------------------
# Set up the environment to configure the snprintv subpackage using
# this version of AutoGen (as opposed to any installed version).
# ----------------------------------------------------------------------
ac_configure_args="$ac_configure_args --enable-snprintfv-convenience"
ag_srcdir=`\cd $srcdir && pwd`
if test x$ag_srcdir != x && test -d $ag_srcdir; then
  :
else
  ag_srcdir=..
fi

# ----------------------------------------------------------------------
# If `configure' is invoked (in)directly via `make', ensure that it
# encounters no `make' conflicts.  Ignore error if shell does not have
# unset, but at least set these to empty values.
# ----------------------------------------------------------------------
MFLAGS=
MAKEFLAGS=
MAKELEVEL=
unset MFLAGS MAKEFLAGS MAKELEVEL 2>/dev/null

AM_WITH_DMALLOC

# ----------------------------------------------------------------------
# check for various programs used during the build.
# ----------------------------------------------------------------------
# =================
# AC_PROG_CC
# =================
AC_PROG_CC
# =================
# AC_PROG_CC_STDC
# =================
AC_PROG_CC_STDC
# =================
# AC_EXEEXT
# =================
AC_EXEEXT
# =================
# AC_PROG_INSTALL
# =================
AC_PROG_INSTALL
# =================
# AC_PROG_LIBTOOL
# =================
AC_PROG_LIBTOOL
# =================
# AC_CHECK_PROG
# =================
AC_CHECK_PROG(TEXI2HTML, texi2html, texi2html, :)
# =================
# AC_C_CONST
# =================
AC_C_CONST
# =================
# AC_C_INLINE
# =================
AC_C_INLINE

# ----------------------------------------------------------------------
# Check for standard headers.
# ----------------------------------------------------------------------
# =================
# AC_HEADER_STDC
# =================
AC_HEADER_STDC
# =================
# AC_HEADER_DIRENT
# =================
AC_HEADER_DIRENT

# ----------------------------------------------------------------------
# AC_CHECK_HEADERS Check for list of headers.
# ----------------------------------------------------------------------
AC_CHECK_HEADERS(errno.h unistd.h fcntl.h memory.h sys/param.h \
 libgen.h sys/time.h setjmp.h )

if   test "${ac_cv_header_stdint_h+set}" = set \
  || test "${ac_cv_header_inttypes_h+set}" = set
then : ; else
  AC_MSG_ERROR([You must have stding. or inttypes.h on your system])
fi

# =================
# AC_CHECK_HEADERS: stdarg.h is present define HAVE_STDARG_H, otherwise
# if varargs.h is present define HAVE_VARARGS_H.
# =================
AC_CHECK_HEADERS(stdarg.h varargs.h, break)

# =================
# Similarly for the string.h and strings.h headers
# =================
AC_CHECK_HEADERS(string.h strings.h, break)

# =================
# ...and limits headers
# =================
AC_CHECK_HEADERS(limits.h sys/limits.h values.h, break)

# =================
# ...and directory read headers
# =================
AC_CHECK_HEADERS(dirent.h ndir.h sys/ndir.h, break)

# =================
# Check for P_PID definition in <sys/procset.h>
# =================
AC_CHECK_HEADERS(sys/procset.h)

# ----------------------------------------------------------------------
# AC_CHECK_LIB for SVR4 libgen, and use it if it defines pathfind.
# ----------------------------------------------------------------------
AC_CHECK_LIB(gen, pathfind)

# ----------------------------------------------------------------------
# Check for Dynamic library stuff
# ----------------------------------------------------------------------
AC_CHECK_HEADERS(dlfcn.h)
AC_CHECK_LIB(dl, dlopen)
AC_CHECK_FUNCS(dlopen)
if test $ac_cv_func_dlopen = yes
then DYNAMIC_AG=-export-dynamic
else DYNAMIC_AG=""
fi

# ----------------------------------------------------------------------
# Checks for typedefs, structures, and compiler characteristics.
# ----------------------------------------------------------------------
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T
AC_TYPE_SIGNAL
# AC_C_LONG_DOUBLE
AC_C_LONG_DOUBLE
if test x$ac_cv_type_long_double = xno; then
  snv_long_double=double
else
  snv_long_double='long double'
fi
# AC_DEFINE_UNQUOTED for SNV_LONG_DOUBLE
AC_DEFINE_UNQUOTED([SNV_LONG_DOUBLE],$snv_long_double,
                   [Define this to the long+double type])
AC_CHECK_TYPES([u_int])
AC_CHECK_TYPES([long long, uintmax_t])
AC_CHECK_SIZEOF(char*, 4)
AC_CHECK_SIZEOF(int, 4)

# ----------------------------------------------------------------------
# Checks for standard functions.
# ----------------------------------------------------------------------
AC_CHECK_FUNCS(strchr setjmp sigsetjmp strsignal strlcpy snprintf)
AC_CHECK_FUNCS(copysign copysignl modfl)

# ----------------------------------------------------------------------
# Do all our own macros
# ----------------------------------------------------------------------
INVOKE_AG_MACROS

if ! test x$ag_cv_sys_siglist = xyes
then
  if ! test x$ac_cv_func_strsignal = xyes
  then
    echo "WARNING:  strsignal will use POSIX names and Linux signal numbers"
  fi
fi >&2

# ----------------------------------------------------------------------
# Do SNPRINTFV macros
# ----------------------------------------------------------------------
INVOKE_SNPRINTFV_MACROS
# ----------------------------------------------------------------------
# Generate the make files.
# ----------------------------------------------------------------------
AC_PROG_LN_S
test -f ./snprintfv/snprintfv/snprintfv.h \
  && rm -f ./snprintfv/snprintfv.h \
  && ${LN_S} `pwd`/snprintfv/snprintfv/snprintfv.h ./snprintfv/

[M4_SRC=`cd $srcdir/config ; echo [a-z]*.m4`]
AC_SUBST(M4_SRC)
[AGFUN_LIST=`cd $srcdir/agen5 ; echo func*.c`]
AC_SUBST(AGFUN_LIST)
AGEXE=autogen${ac_exeext}
AC_SUBST(AGEXE)
GDEXE=getdefs${ac_exeext}
AC_SUBST(GDEXE)
COLEXE=columns${ac_exeext}
AC_SUBST(COLEXE)
AC_SUBST(DYNAMIC_AG)
AC_SUBST(ac_aux_dir)
AC_SUBST(LIBS)
[
if test "$top_srcdir" = "$top_builddir"
then
 INCLIST='-I${top_builddir} -I${top_srcdir}/autoopts'
else
 INCLIST='-I${top_builddir} -I${top_srcdir}'
 INCLIST="${INCLIST} -I\${top_builddir}/autoopts -I\${top_srcdir}/autoopts"
fi
]
AC_SUBST(INCLIST)
AC_DEFINE( [AUTOGEN_BUILD], 1, [Define this if we are building AutoGen] )

AM_CONFIG_HEADER(config.h:config-h.in)
==ADD_CONFIG_FILES==

AC_CONFIG_COMMANDS([stamp-h],
[test -z "$CONFIG_HEADERS" || echo timestamp > stamp-h])
AC_OUTPUT

echo \
"------------------------------------------------------------------------
Configuration:

  Source code location:   ${srcdir}
  Compiler:               ${CC}
  Compiler flags:         ${CFLAGS}
  Host System Type:       ${host}
  Install path:           ${prefix}

  See config.h for further configuration information.
------------------------------------------------------------------------"

dnl configure.in ends here
