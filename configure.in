dnl  -*- Mode: M4 -*- 

dnl --------------------------------------------------------------------
dnl configure.in --- GNU autoconf source for toplevel directory.
dnl
dnl Copyright (C) 1997-1999, 2001 Bruce Korb
dnl
dnl Author:           Bruce Korb <bkorb@gnu.org>
dnl Maintainer:       Bruce Korb <bkorb@gnu.org>
dnl Created:          Mon Jun 30 15:10:13 1997
dnl Last Modified:    $Date: 2001/07/15 23:07:21 $
dnl            by:    Bruce Korb <bkorb@gnu.org>
dnl --------------------------------------------------------------------
dnl Code: $Id: configure.in,v 2.46 2001/07/15 23:07:21 bkorb Exp $
dnl --------------------------------------------------------------------

AC_PREREQ(2.50)
AC_INIT
AC_CONFIG_SRCDIR(agen5/autogen.c)

. $srcdir/VERSION

AC_CONFIG_AUX_DIR(config)

AC_CANONICAL_TARGET
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

ifdef([AC_REVISION],AC_REVISION($Revision: 2.46 $),)dnl

# ----------------------------------------------------------------------
# Substitute VERSION vars here, so that they can be used by the Makefile
# ----------------------------------------------------------------------
AC_SUBST(AG_VERSION)
AC_SUBST(AG_MAJOR_VERSION)
AC_SUBST(AG_MINOR_VERSION)
AC_SUBST(AO_CURRENT)
AC_SUBST(AO_REVISION)
AC_SUBST(AO_AGE)
AC_SUBST(GO_CURRENT)
AC_SUBST(GO_REVISION)
AC_SUBST(GO_AGE)
AC_SUBST(AO_ANNOUNCE_LEVEL)
AC_DEFINE_UNQUOTED(AO_CURRENT,$AO_CURRENT,
                   [Define this to the autoopts current interface number])
AC_DEFINE_UNQUOTED(AO_REVISION,$AO_REVISION,
                   [Define this to the autoopts interface revision number])
AC_DEFINE_UNQUOTED(AO_AGE,$AO_AGE,
                   [Define this to the autoopts interface age number])

AC_DEFINE_UNQUOTED(AO_ANNOUNCE_LEVEL,$AO_ANNOUNCE_LEVEL,
                   [AutoOpts will announce itself at this level])
# ----------------------------------------------------------------------
# Set up the environment to configure the snprintv subpackage using
# this version of AutoGen (as opposed to any installed version).
# ----------------------------------------------------------------------
ac_configure_args="$ac_configure_args --enable-snprintfv-convenience"
ag_srcdir=`\cd $srcdir && pwd`
if test x$ag_srcdir != x && test -d $ag_srcdir; then
  :
else
  ag_srcdir=..
fi
AC_CONFIG_SUBDIRS(snprintfv)

# ----------------------------------------------------------------------
# If `configure' is invoked (in)directly via `make', ensure that it
# encounters no `make' conflicts.  Ignore error if shell does not have
# unset, but at least set these to empty values.
# ----------------------------------------------------------------------
MFLAGS=
MAKEFLAGS=
MAKELEVEL=
unset MFLAGS MAKEFLAGS MAKELEVEL 2>/dev/null

# ----------------------------------------------------------------------
# check for various programs used during the build.
# ----------------------------------------------------------------------
AC_PROG_CC
AM_PROG_CC_STDC
AC_EXEEXT
ifdef([AC_LIBTOOL_WIN32_DLL],
   AC_LIBTOOL_WIN32_DLL)
AC_PROG_INSTALL
AM_PROG_LIBTOOL
AG_PROG_BYACC
AC_CHECK_PROG(TEXI2HTML, texi2html, texi2html, :)

# ----------------------------------------------------------------------
# Set up and process configure options
# ----------------------------------------------------------------------
AG_CHECK_LIB_REGEX

AC_MSG_CHECKING([whether runtime debugging is wanted])
ag_cv_debug=no
AC_ARG_ENABLE(debug,
[  --enable-debug          enable/disable debugging code [no]],[
ag_cv_debug=$enable_debug
if test x$enable_debug = xyes; then
  CPPFLAGS="-DDEBUG $CPPFLAGS"
  CFLAGS="-g `echo $CFLAGS|sed 's%-g *%%g'`"
  if test x$GCC != xyes; then
changequote(,)
    CFLAGS=`echo $CFLAGS|sed 's%-O[0-9]* *%%g'`
changequote([,])
  fi
fi])
AC_MSG_RESULT([$ag_cv_debug])

AM_WITH_DMALLOC

# ----------------------------------------------------------------------
# Check for standard headers.
# ----------------------------------------------------------------------
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_CHECK_HEADERS(errno.h unistd.h fcntl.h memory.h sys/file.h sys/param.h)
AC_CHECK_HEADERS(sys/types.h libgen.h sys/stat.h sys/time.h setjmp.h stdlib.h)

# If stdarg.h is present define HAVE_STDARG_H, otherwise if varargs.h
# is present define HAVE_VARARGS_H.
AC_CHECK_HEADERS(stdarg.h varargs.h, break)

# Similarly for the string.h and strings.h headers
AC_CHECK_HEADERS(string.h strings.h, break)

# ...and limits headers
AC_CHECK_HEADERS(limits.h sys/limits.h, break)

# ...and directory read headers
AC_CHECK_HEADERS(dirent.h ndir.h sys/ndir.h, break)

# Check for P_PID definition in <sys/procset.h>
AC_CHECK_HEADERS(sys/procset.h)

# ----------------------------------------------------------------------
# Check for SVR4 libgen, and use it if it defines pathfind.
# ----------------------------------------------------------------------
AC_CHECK_LIB(gen, pathfind)

# ----------------------------------------------------------------------
# Check for Dynamic library stuff
# ----------------------------------------------------------------------
AC_CHECK_HEADERS(dlfcn.h)
AC_CHECK_LIB(dl, dlopen)
AC_CHECK_FUNCS(dlopen)
if test $ac_cv_func_dlopen = yes
then DYNAMIC_AG=-export-dynamic
else DYNAMIC_AG=""
fi

# ----------------------------------------------------------------------
# Checks for typedefs, structures, and compiler characteristics.
# ----------------------------------------------------------------------
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T
AC_TYPE_SIGNAL
AC_CHECK_TYPE(u_short, unsigned short int)
AC_CHECK_TYPE(u_int, unsigned int)
AC_CHECK_TYPE(u_long, unsigned long int)
AC_CHECK_TYPE(id_t, int)
AC_CHECK_TYPE(wchar_t, unsigned int)
AC_C_CONST
AC_C_INLINE
AC_CHECK_SIZEOF(char*, 4)
AC_CHECK_SIZEOF(int, 4)

# ----------------------------------------------------------------------
# Checks for standard functions.
# ----------------------------------------------------------------------
AC_CHECK_FUNCS(strchr setjmp sigsetjmp strsignal psignal pathfind strlcpy)
AC_CHECK_FUNCS(snprintf)
AG_FUNC_FOPEN_BINARY
AG_FUNC_FOPEN_TEXT
AG_CHECK_ALLOCATED_CTIME
AG_CHECK_STRFTIME
AG_CHECK_SYS_SIGLIST

# ----------------------------------------------------------------------
# Generate the make files.
# ----------------------------------------------------------------------
test -f ./snprintfv/snprintfv/snprintfv.h \
  && rm -f ./snprintfv/snprintfv.h \
  && ln -s `pwd`/snprintfv/snprintfv/snprintfv.h ./snprintfv/
GUILE_LINK=`guile-config link`
if test -z "$GUILE_LINK"
then
    echo 'Cannot find `guile-config'\' configurator >&2
    exit 1
fi
AC_SUBST(GUILE_LINK)
GUILE_INC=`guile-config compile | sed 's,-I */usr/include,,'`
AC_SUBST(GUILE_INC)

[M4_SRC=`cd $srcdir/config ; echo [a-z]*.m4`]
AC_SUBST(M4_SRC)
[AGFUN_LIST=`cd $srcdir/agen5 ; echo func*.c`]
AC_SUBST(AGFUN_LIST)
AGEXE=autogen${ac_exeext}
AC_SUBST(AGEXE)
GDEXE=getdefs${ac_exeext}
AC_SUBST(GDEXE)
COLEXE=columns${ac_exeext}
AC_SUBST(COLEXE)
AC_SUBST(DYNAMIC_AG)
AC_SUBST(ac_aux_dir)
AC_SUBST(LIBS)
[
if test "$top_srcdir" = "$top_builddir"
then
 INCLIST="-I${top_builddir} -I${top_srcdir}/autoopts -I${top_srcdir}/snprintfv"
else
 INCLIST="-I${top_builddir} -I${top_srcdir}"
 INCLIST="${INCLIST} -I${top_builddir}/autoopts -I${top_srcdir}/autoopts"
 INCLIST="${INCLIST} -I${top_builddir}/snprintfv -I${top_srcdir}/snprintfv"
fi
]
AC_SUBST(INCLIST)

AM_CONFIG_HEADER(config.h:config-h.in)
AC_CONFIG_FILES([Makefile agen5/Makefile agen5/test/Makefile \
autoopts/Makefile autoopts/autoopts-config autoopts/test/Makefile \
columns/Makefile compat/Makefile config/Makefile config/autogen.spec \
doc/Makefile getdefs/Makefile getdefs/test/Makefile])

AC_CONFIG_COMMANDS([stamp-h],
[test -z "$CONFIG_HEADERS" || echo timestamp > stamp-h])
AC_OUTPUT

echo \
"------------------------------------------------------------------------
Configuration:

  Source code location:       ${srcdir}
  Compiler:	              ${CC}
  Compiler flags:             ${CFLAGS}
  Host System Type:           ${host}
  Install path:		      ${prefix}

  See config.h for further configuration information.
------------------------------------------------------------------------"

dnl configure.in ends here
