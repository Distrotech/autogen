dnl  -*- Mode: M4 -*- 
dnl --------------------------------------------------------------------
dnl configure.in --- GNU autoconf source for toplevel directory.
dnl
dnl Copyright (C) 1997-1998 Bruce Korb
dnl
dnl Author:           Bruce Korb <korbb@datadesign.com>
dnl Maintainer:       Bruce Korb <korbb@datadesign.com>
dnl Created:          Mon Jun 30 15:10:13 1997
dnl Last Modified:    Tue Nov 24 09:30:32 1998
dnl            by:    Bruce Korb <korb@datadesign.com>
dnl --------------------------------------------------------------------
dnl Code: $Id: configure.in,v 2.5 1998/12/08 18:34:28 bkorb Exp $
dnl --------------------------------------------------------------------

AC_INIT(src/autogen.c)
. $srcdir/VERSION

AC_CONFIG_AUX_DIR(config)
AC_CANONICAL_SYSTEM

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

ifdef([AC_REVISION],AC_REVISION($Revision: 2.5 $),)dnl

# ----------------------------------------------------------------------
# Substitute VERSION vars here, so that they can be used by the Makefile
# ----------------------------------------------------------------------
AC_SUBST(AG_VERSION)
AC_SUBST(AG_MAJOR_VERSION)
AC_SUBST(AG_MINOR_VERSION)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

# ----------------------------------------------------------------------
# If `configure' is invoked (in)directly via `make', ensure that it
# encounters no `make' conflicts.  Ignore error if shell does not have
# unset, but at least set these to empty values.
# ----------------------------------------------------------------------
MFLAGS=
MAKEFLAGS=
MAKELEVEL=
unset MFLAGS MAKEFLAGS MAKELEVEL 2>/dev/null

# ----------------------------------------------------------------------
# check for various programs used during the build.
# ----------------------------------------------------------------------
AC_PROG_CC
ifdef([AC_EXEEXT],[AC_EXEEXT],[AM_CYGWIN32])
ifdef([AC_PROG_INSTALL],[AC_PROG_INSTALL],[AM_PROG_INSTALL])
AM_PROG_LIBTOOL
AC_PROG_YACC
AG_PROG_MAKE_INCLUDE
AC_CHECK_PROG(TEXI2HTML, texi2html, texi2html, :)

# ----------------------------------------------------------------------
# Set up and process configure options
# ----------------------------------------------------------------------
AG_CHECK_LIB_REGEX

AC_MSG_CHECKING([whether runtime debugging is wanted])
ag_cv_debug=no
AC_ARG_ENABLE(debug,
[  --enable-debug          enable/disable debugging code [no]],[
ag_cv_debug=$enable_debug
if test x$enable_debug = xyes; then
  CPPFLAGS="-DDEBUG $CPPFLAGS"
  CFLAGS="-g `echo $CFLAGS|sed 's%-g *%%g'`"
  if test x$GCC != xyes; then
changequote(,)
    CFLAGS=`echo $CFLAGS|sed 's%-O[0-9]* *%%g'`
changequote([,])
  fi
fi])
AC_MSG_RESULT([$ag_cv_debug])

AM_WITH_DMALLOC

# ----------------------------------------------------------------------
# Check for standard headers.
# ----------------------------------------------------------------------
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_CHECK_HEADERS(errno.h unistd.h fcntl.h memory.h sys/file.h sys/param.h)
AC_CHECK_HEADERS(sys/types.h libgen.h sys/stat.h sys/time.h setjmp.h stdlib.h)

# If varargs.h is present define HAVE_VARARGS_H, otherwise if stdarg.h
# is present define HAVE_STDARG_H.
AC_CHECK_HEADERS(varargs.h stdarg.h, break)

# Similarly for the string.h and strings.h headers
AC_CHECK_HEADERS(string.h strings.h, break)

# ...and limits headers
AC_CHECK_HEADERS(limits.h sys/limits.h, break)

# ...and directory read headers
AC_CHECK_HEADERS(dirent.h ndir.h sys/ndir.h, break)

# Check for P_PID definition in <sys/procset.h>
AC_CHECK_HEADERS(sys/procset.h)

# ----------------------------------------------------------------------
# Check for SVR4 libgen, and use it if it defines pathfind.
# ----------------------------------------------------------------------
AC_CHECK_LIB(gen, pathfind)

# ----------------------------------------------------------------------
# On most SVR3's, alloca() is tucked inside libPW.
# ----------------------------------------------------------------------
AC_CHECK_LIB(PW, alloca)

# ----------------------------------------------------------------------
# Checks for typedefs, structures, and compiler characteristics.
# ----------------------------------------------------------------------
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T
AC_TYPE_SIGNAL
AC_CHECK_TYPE(u_short, unsigned short int)
AC_CHECK_TYPE(u_int, unsigned int)
AC_CHECK_TYPE(u_long, unsigned long int)
# AC_CHECK_TYPE(wchar_t, unsigned in)
AC_C_CONST
AC_C_INLINE
AC_CHECK_SIZEOF(char*, 4)
AC_CHECK_SIZEOF(int, 4)

# ----------------------------------------------------------------------
# Checks for standard functions.
# ----------------------------------------------------------------------
AC_CHECK_FUNCS(strchr setjmp sigsetjmp strsignal sigsend pathfind)
AC_FUNC_ALLOCA
AG_FUNC_FOPEN_BINARY
AG_FUNC_CTIME_HEAP
AG_FUNC_STRFTIME
AG_FUNC_VSNPRINTF_DOLLAR
AG_FUNC_SNPRINTF_DOLLAR

# ----------------------------------------------------------------------
# Generate the make files.
# ----------------------------------------------------------------------
[COMPAT_SRC=`cd $srcdir/compat ; echo [a-z]*[!~]`]
AC_SUBST(COMPAT_SRC)
[M4_SRC=`cd $srcdir/config ; echo [a-z]*.m4`]
AC_SUBST(M4_SRC)
[AGFUN_LIST=`cd $srcdir/src ; echo agFun*.c`]
AC_SUBST(AGFUN_LIST)
[TEST_LIST=`cd $srcdir/tests ; echo [a-z]*.test`]
AC_SUBST(TEST_LIST)
AC_SUBST(ac_aux_dir)
AC_SUBST(LIBS)
AC_SUBST(INCLUDES)
AM_CONFIG_HEADER(config.h)
AC_OUTPUT(Makefile MakeRules doc/Makefile compat/Makefile \
autoopts/Makefile autoopts/autoopts-config src/Makefile tests/Makefile \
config/Makefile getdefs/Makefile columns/Makefile,
 [test -z "$CONFIG_HEADERS" || echo timestamp > stamp-h])

echo \
"------------------------------------------------------------------------
Configuration:

  Source code location:       ${srcdir}
  Compiler:	              ${CC}
  Compiler flags:             ${CFLAGS}
  Host System Type:           ${host}
  Install path:		      ${prefix}

  See config.h for further configuration information.
------------------------------------------------------------------------"

dnl configure.in ends here
