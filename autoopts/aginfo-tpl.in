[= AutoGen5 template texi

##  Documentation template
##
##  AutoGen Copyright (C) 1992-2001 Bruce Korb
##
## Author:            Bruce Korb <bkorb@gnu.org>
## Maintainer:        Bruce Korb <bkorb@gnu.org>
## Created:           Tue Sep 15 13:13:48 1998
## Last Modified:     $Date: 2001/09/22 04:05:08 $
##            by:     Bruce Korb <bkorb@gnu.org>
## ---------------------------------------------------------------------
## $Id: aginfo-tpl.in,v 2.22 2001/09/22 04:05:08 bkorb Exp $
## ---------------------------------------------------------------------
##
=]
@node [=

prog_name

=] Invocation
@[=
 (getenv "LEVEL")
 =] Invoking [=
prog_name
=]
@cindex [= % prog_name
 (define down-prog-name (string-downcase! "%s"))
 (. down-prog-name) =] invocation
@ignore
[=
(out-push-new (shellf "echo %s | sed 's/texi$/menu/'" (out-name)) )
=]* [= % prog_name (sprintf "%%-32s"  "%s Invocation::")
     =] Invoking [=prog_name=]
[=(out-pop)
  (dne "# " "# ")=]
@end ignore
[=

IF (exist? "prog_info_descrip")  =]
[= prog_info_descrip =][=

ELSE =]
This [=(string-downcase! (getenv "LEVEL"))=] documents the @code{AutoOpts}
generated usage text and option meanings for [=prog_name=].[=
ENDIF=][=

IF (exist? "copyright") =]

This software is released under [=
  CASE copyright.type =][=
   =  gpl =]the GNU General Public License[=
   = lgpl =]the GNU General Public License with Library Extensions[=
   =  bsd =]the Free BSD License[=
   *      =]a specialized copyright license[=
  ESAC =].[=
ENDIF =]

@menu
* [=(sprintf "%s %-24s" (. down-prog-name) "usage::")
  =] [=prog_name=] usage help[=
     (if (exist? "flag.value") " (-?)") =]
[=(out-push-new (string-append (out-name) ".tmp"))
  (shell (string-append "f=" (out-name))) =][=
FOR flag =][=
  IF (not (exist? "documentation"))
=]* [=(sprintf
         "%s %-24s"
         (. down-prog-name)
         (string-append
            (string-tr! (get "name") "A-Z^_" "a-z--" )
            "::" ) )
     =] [=% name (string-tr! "%s" "A-Z^_" "a-z--")=] option[=
          % value " (-%s)" =]
[=ENDIF =][=
ENDFOR flag=][=
(out-pop) =][=
`sort $f ; rm -f $f` =]
@end menu

@node [=(. down-prog-name)=] usage
@[=CASE (getenv "LEVEL") =][=
   = chapter    =][=
   = section    =]sub[=
   = subsection =]subsub[=
   ESAC =]section [=prog_name=] usage help[=
     (if (exist? "flag.value") " (-?)") =]
@cindex [=(. down-prog-name)=] usage

This is the automatically generated usage text for [=prog_name=]:

@exampleindent 0
@example
[= (shellf "PROG=./%1$s

    if [ ! -f ${PROG} ]
    then PROG=./`echo $PROG | tr '[A-Z]' '[a-z]'` ; fi

    if [ ! -f ${PROG} ]
    then PROG=./`echo $PROG | tr x_ x-` ; fi

    if [ ! -f ${PROG} ]
    then PROG='echo %1$s is unavailable - no' ; fi

    ${PROG} %2$s 2>&1 | \
        sed -e 's/USAGE:  lt-/USAGE:  /' \
            -e 's/{/@{/g' -e 's/}/@}/g' \
            -e 's/\t/        /g' "

    (get "prog_name")

    (if (exist? "long_opts")       "--help"
        (if (exist? "flag.value")  "-?"
                                   "help" ))

    )  =]
@end example
@exampleindent 4[=

#  FOR all options, except the `documentation' options, ...  =][=

IF (define opt-name "")
   (exist? "preserve-case")     =][=
   (define optname-from "_^")
   (define optname-to   "--")   =][=
ELSE                            =][=
  (define optname-from "A-Z_^")
  (define optname-to   "a-z--") =][=
ENDIF                           =][=

FOR flag=][=

  IF (not (exist? "documentation")) =]

@node [=
    (set! opt-name (string-tr! (get "name") optname-from optname-to))
    (string-append down-prog-name " " opt-name)=]
@[=CASE (getenv "LEVEL") =][=
   = chapter    =][=
   = section    =]sub[=
   = subsection =]subsub[=
   ESAC =]section [=(. opt-name)=] option[=
          % value " (-%s)" =]
@cindex [=(. down-prog-name)=]-[=(. opt-name)=]

[=descrip=].[=

    IF (exist? "min") =]
This option is required to appear.[=
    ENDIF=][=

    IF (exist? "max") =]
This option may appear [=
      IF % max (= "%s" "NOLIMIT")
       =]an unlimited number of times[=
      ELSE
       =]up to [=max=] times[=
      ENDIF=].[=
    ENDIF=][=

    IF (exist? "enabled") =]
This option is enabled by default.[=
    ENDIF=][=

    IF (exist? "no_preset") =]
This option may not be preset with environment variables
or in initialization (rc) files.[=
    ENDIF=][=

    IF (exist? "equivalence") =]
This option is a member of the [=equivalence=] class of options.[=
    ENDIF=][=

    IF (exist? "flags_must") =]
This opton must appear in combination with the following options:
[=     FOR flags_must ", " =][=flags_must=][=
       ENDFOR=].[=
    ENDIF=][=

    IF (exist? "flags_cant") =]
This option must not appear in combination with any of the following options:
[=     FOR flags_cant ", " =][=flags_cant=][=
       ENDFOR=].[=
    ENDIF=]

[= ?% doc '%s' "This option has no @samp{doc} documentation." =][=


  ENDIF  `doc' exists =][=
ENDFOR flag=][=

aginfo.tpl ends here =]
